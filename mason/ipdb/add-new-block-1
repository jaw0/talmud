%# -*- mason -*-
%# Copyright (c) 2003 by Jeff Weisberg
%# Author: Jeff Weisberg <jaw @ tcp4me.com>
%# Date: 2003-Oct-30 12:50 (EST)
%# Function: 
%#
%# $Id: add-new-block-1,v 1.9 2010/01/16 03:22:05 jaw Exp $

<%attr>
    title   => 'Add New Block'
    acl     => 'ipdbadmin'
</%attr>
%################################################################
<%args>

</%args>
%################################################################
<%init>
    my $s   = Talmud::sess();
    my $db  = Talmud::db();
    my $cid = Talmud::g_obj_next_id($db);

    my $block;
    my $form = $m->comp('add-new-block:FORM');

    $m->comp('/sys/form:fixup', form => $form, args => \%ARGS);
    my $err = $m->comp('/sys/form:validate', form => $form, args => \%ARGS);

    if( !$err && IPDB::valid_net_and_len( $ARGS{block}, $ARGS{size} ) ){
	$err->{block} = 'invalid netblock for specified masklen'
    }

    if( $err ){
	return $m->comp( 'add-new-block', err => $err, dfl => \%ARGS );
    }

    eval {
	$db->start();
        Talmud::create_object( $db, 'ipdb/meta', $cid );
        my $data = $m->comp('/sys/form:sql_from_form', form => $form, args => \%ARGS, sqlcol => 'sql');
	$data->{meta_id} = $cid;

	unless( $data->{swipprefix} ){
	    # pick a nice usable swip prefix
	    my $mntr = $db->select_scalar(
			'select maintainer from ipdb_registry where registry_id = ?',
			$data->{registry_id} );
	    my @a = localtime;
	    $data->{swipprefix} = sprintf "$mntr%d%0.2d", $a[5] - 100, $a[4];
	}

	$db->insert_hash('mi_ipdb_meta', $data );

	$block = Talmud::create_object($db, 'ipdb/block');
	$db->insert_hash('mi_ipdb_block', {
		meta_id		=> $cid,
		block_id	=> $block,
		netblock	=> $data->{netblock},
		size		=> $data->{size},
		status		=> 'available',
		pool		=> ($ARGS{pool} || undef),
		sort_low	=> IPDB::aton( $data->{netblock} ),
                sort_high       => IPDB::aton( $data->{netblock} ) | ~ IPDB::len_2_mask($data->{size}),
	} );

        $db->commit();
    };

    if( $@ ){
	my $emsg = $@;
	$emsg = $db->error_sanitize( $emsg );
	$db->rollback();
	$m->comp('/sys/error.mas', msg => $emsg);
	return $m->comp( 'add-new-block', dfl => \%ARGS );
    }

    $m->comp('/sys/redirect.mas', where => "/ipdb/view-meta?oid=$cid" );
</%init>
